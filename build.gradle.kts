plugins {
    id("java")
    id("org.jetbrains.kotlin.jvm") version "1.9.23"
    id("org.jetbrains.intellij") version "1.17.3"
}

group = "com.example"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    implementation("com.google.code.gson:gson:2.10.1")
}

// Configure Gradle IntelliJ Plugin
// Read more: https://plugins.jetbrains.com/docs/intellij/tools-gradle-intellij-plugin.html
intellij {
    version.set("232.10300.40.2321.11567975")
    type.set("AI")  //Target IDE Platform
    plugins.set(listOf("android"))
}

tasks {
    buildSearchableOptions {
        enabled = false
    }

    // This block is needed to specify backwards compatibility
    patchPluginXml {
        version.set("${project.version}")
        sinceBuild.set("221")
        untilBuild.set("233.*")
    }
    // This is a gradle task to run the "plugin verifier" when publishing to the Jetbrains Marketplace locally.
    // Checking against "Electric Eel | 2022.1.1 Patch 2", can add more versions here as needed
    runPluginVerifier {
        ideVersions.set(listOf("221.6008.13"))
    }
    runIde {
        ideDir.set(file("/Applications/Android Studio.app/Contents"))
    }

    // Set the JVM compatibility versions
    withType<JavaCompile> {
        sourceCompatibility = "17"
        targetCompatibility = "17"
    }
    withType<org.jetbrains.kotlin.gradle.tasks.KotlinCompile> {
        kotlinOptions.jvmTarget = "17"
    }

    //https://plugins.jetbrains.com/docs/intellij/plugin-signing.html
    signPlugin {
        //generated using openssl: https://plugins.jetbrains.com/docs/intellij/plugin-signing.html#generate-private-key
        certificateChain.set(System.getenv("AS_BOOKMARKS_PROJECT_Certificate_Chain"))
        privateKey.set(System.getenv("AS_BOOKMARKS_PROJECT_Private_Key"))

        //custom (generated by us)
        password.set(System.getenv("AS_BOOKMARKS_PROJECT_Private_Key_Password"))
    }

    //https://plugins.jetbrains.com/docs/intellij/publishing-plugin.html
    publishPlugin {
        //from https://plugins.jetbrains.com/author/me/tokens
        token.set(System.getenv("AS_BOOKMARKS_PROJECT_Intellij_Publish_Token"))
    }
}